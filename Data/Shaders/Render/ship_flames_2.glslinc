###VERTEX

#version 120
#extension GL_ARB_draw_instanced : enable

#define in attribute
#define out varying

// Inputs
in vec4 inFlame1; // Position, PlaneId, FlamePersonalitySeed
in vec2 inFlame2; // FlameSpacePosition

// Outputs
out vec2 flameSpacePosition;
out float noBorder;
out vec2 noiseOffset;

// Params
uniform mat4 paramOrthoMatrix;
uniform float paramFlameSpeed;

void main()
{
    flameSpacePosition = inFlame2.xy;
    noBorder = float(gl_InstanceIDARB);
    noiseOffset = vec2(inFlame1.w, inFlame1.w - paramFlameSpeed / 0.8);

    gl_Position = paramOrthoMatrix * vec4(inFlame1.xyz, 1.0);
}

###FRAGMENT

#version 120

#define in varying

// Inputs from previous shader
in vec2 flameSpacePosition; // (x=[-0.5, 0.5], y=[0.0, 1.0])
in float noBorder;
in vec2 noiseOffset;

// The texture
uniform sampler2D paramNoiseTexture1;

// Params
uniform float paramFlameWindRotationAngle;

//
// Based on "Flame in the Wind" by kuvkar (https://www.shadertoy.com/view/4tXXRn)
//

mat2 GetRotationMatrix(float angle)
{
    mat2 m;
    m[0][0] = cos(angle); m[0][1] = -sin(angle);
    m[1][0] = sin(angle); m[1][1] = cos(angle);
    return m;
}

void main()
{
    //
    // Apply wind
    //

    // Rotation angle is higher the higher we go
    float windAngle = paramFlameWindRotationAngle * flameSpacePosition.y;
    
    // Rotate around bottom
    vec2 uv = GetRotationMatrix(windAngle) * flameSpacePosition - vec2(0.0, 0.5);


    //
    // Get noise for this fragment and time
    //

    #define NoiseResolution 0.75
    vec2 noiseUv = uv * NoiseResolution + noiseOffset;
    float fragmentNoise = texture2D(paramNoiseTexture1, noiseUv).r * 0.5 + 0.25; // -> (0.25, 0.75)


    //
    // Rotate fragment based on noise and vertical extent
    //
    
    float angle = (fragmentNoise - 0.5);

    // Magnify rotation amount based on distance from center of quad
    angle /= max(0.1, length(uv));

    // Straighten the flame at the bottom and make full turbulence higher up
    angle *= smoothstep(-0.3, 0.5, flameSpacePosition.y);

    // Smooth the angle
    angle *= 0.45;

    // Rotate!
    uv += GetRotationMatrix(angle) * uv;    
    

    //
    // Calculate thickness
    //
    
    #define FlameWidth 0.5
    float thickness = 1.0 - smoothstep(0.1, FlameWidth, abs(uv.x));
    
    // Taper flame depending on randomized height
    thickness *= 1.0 - smoothstep(fragmentNoise * 0.7, 1.3, flameSpacePosition.y); // Taper up
    thickness *= smoothstep(-0.15, 0.15, flameSpacePosition.y); // Taper down

    // Discard if we are on the edge, i.e. if:
    // thickness < 0.427995 => focusedThickness < 0.28 (@FlameFocus=1.5)
    if (thickness < 0.427995)
        discard;


    //
    // Emit
    //

    // Core1 (white->yellow)
    vec4 col1 = mix(vec4(1.0, 0.764, 0.1, 0.0), vec4(1.0, 1.0, 1.0, 0.0), smoothstep(0.82, 1.2, thickness));

    // Core2 (->orange)
    col1 = mix(vec4(1.0, 0.35, 0.0, 0.0), col1, smoothstep(0.65, 1.18, thickness));

    // Border (->dark red)
    col1 = mix(vec4(0.3, 0.0, 0.0, 0.0), col1, smoothstep(0.448, 0.671, thickness) + noBorder);

    // Blend with background
    // When no border: transparent < 0.53  --- opaque > 0.86
    // When border:    transparent < 0.0 --- opaque > 0.7
    float alpha = smoothstep(0.0 + noBorder * 0.53, 0.7 + noBorder * 0.16, thickness);

    // Make final color
    vec3 constantList = vec3(1.0f, 0.0f, alpha);
    gl_FragColor = col1.xyzw * constantList.xxxy + constantList.yyyz;
} 
