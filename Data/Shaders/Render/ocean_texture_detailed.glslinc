###VERTEX-120

#define in attribute
#define out varying

// Inputs
in vec3 inOceanDetailed1;	// Position (vec2), yTexture (float)
in vec4 inOceanDetailed2;	// yBack/yMid/yFront/dYFront2 (float, world y)

// Parameters
uniform mat4 paramOrthoMatrix;

// Outputs
out vec2 worldCoords;
out float yTexture;
out vec4 yVectorWorld; // yBackWorld/yMidWorld/yFrontWorld/dYFront2World

void main()
{
    gl_Position = paramOrthoMatrix * vec4(inOceanDetailed1.xy, -1.0, 1.0);
    worldCoords = inOceanDetailed1.xy;
    yTexture = inOceanDetailed1.z;
    yVectorWorld = inOceanDetailed2;
}


###FRAGMENT-120

#define in varying

#include "ocean_surface.glslinc"

// Inputs from previous shader
in vec2 worldCoords;
in float yTexture;
in vec4 yVectorWorld; // yBackWorld/yMidWorld/yFrontWorld/dYFront2World

// The texture
uniform sampler2D paramOceanTexture;

// Parameters
uniform float paramEffectiveAmbientLightIntensity;
uniform float paramOceanTransparency;
uniform vec2 paramTextureScaling;
uniform float paramOceanDarkeningRate;

void main()
{
    // Get texture color sample
    vec2 textureCoord2 = vec2(worldCoords.x, yTexture);
    float darkMix = 1.0 - exp(min(0.0, worldCoords.y) * paramOceanDarkeningRate); // Darkening is based on world Y (more negative Y, more dark)
    vec3 textureColor = mix(
        texture2D(paramOceanTexture, textureCoord2 * paramTextureScaling).xyz,
        vec3(0.),
        darkMix * darkMix * darkMix);

    // Apply detail
    vec4 color = CalculateOceanPlaneColor(
        vec4(textureColor, (1.0 - paramOceanTransparency)),
        worldCoords.y,
        yVectorWorld.x, 
        yVectorWorld.y, 
        yVectorWorld.z, yVectorWorld.w);

    // Combine
    gl_FragColor = vec4(color.xyz * paramEffectiveAmbientLightIntensity, color.w);
}
