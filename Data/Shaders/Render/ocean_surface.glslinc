vec4 CalculateOceanPlaneColor(
    vec4 baseColor, 
    float yWorld, 
    float yBackWorld, float d2YBackWorld,
    float yMidWorld, float d2YMidWorld,
    float yFrontWorld, float d2YFrontWorld)
{
    //
    // Calculate border width
    //
    // We use the second derivative to offset the border width vertically
    //
    
    float dWorld = dFdy(yWorld); // World per pixel
    float basalBorderWidth = 1.5 * dWorld; // pixels

#ifdef RENDER_ALL_PLANES
    float borderWidthHalfBack = basalBorderWidth + abs(dFdx(yBackWorld)) * 1.2;
    float yBackMidBorder = yBackWorld - borderWidthHalfBack;

    float borderWidthHalfMid = basalBorderWidth + abs(dFdx(yMidWorld)) * 1.2;
    float yMidMidBorder = yMidWorld - borderWidthHalfMid;
#endif

    float borderWidthHalfFront = basalBorderWidth + abs(dFdx(yFrontWorld)) * 1.2;
    float yFrontMidBorder = yFrontWorld + borderWidthHalfFront * d2YFrontWorld / (dWorld * 70.);

    //
    // Color
    //

    // 1.0 when yWorld is below middle of the border, 0.0 otherwise

#ifdef RENDER_ALL_PLANES
    float baseColorBack = step(yWorld, yBackMidBorder);
    float baseColorMid = step(yWorld, yMidMidBorder);
#endif

    float baseColorFront = step(yWorld, yFrontMidBorder);

#ifdef RENDER_ALL_PLANES
    float baseColorAlpha =
        max(
            1. * baseColorFront,
            max(
                .6 * baseColorMid,
                .5 * baseColorBack));
#else
    float baseColorAlpha = 1. * baseColorFront;
#endif

    //return vec4(baseColor, baseColorAlpha);

    //
    // Border
    //

#ifdef RENDER_ALL_PLANES
    float borderBack =
        1. - smoothstep(0., borderWidthHalfBack, abs(yWorld - yBackMidBorder));

    float borderMid =
        1. - smoothstep(0., borderWidthHalfMid, abs(yWorld - yMidMidBorder));
#endif

    float borderFront =
        1. - smoothstep(0., borderWidthHalfFront, abs(yWorld - yFrontMidBorder));

#ifdef RENDER_ALL_PLANES
    // epsilon when front is visible, 1 otherwise
    #define FrontObscuredAlpha 0.4
    float borderFrontOpaqueness = (1. - baseColorFront) * (1. - FrontObscuredAlpha) + FrontObscuredAlpha;

    // epsilon when mid is visible, 1 otherwise
    #define MidObscuredAlpha 0.4
    float borderMidOpaqueness = (1. - baseColorMid) * (1. - MidObscuredAlpha) + MidObscuredAlpha;
#endif

#ifdef RENDER_ALL_PLANES
    float borderAlpha =
        max(
            borderFront,
            max(
                .9 * borderMid * borderFrontOpaqueness,
                .7 * borderBack * borderFrontOpaqueness * borderMidOpaqueness));
#else
    float borderAlpha = borderFront;
#endif

    //return vec4(1., 1., 1., borderAlpha);

    // Combine

    vec4 finalColor = mix(
        vec4(baseColor.xyz, baseColorAlpha * baseColor.w),
        vec4(1.), // Border color
        borderAlpha);

    return finalColor;
}
