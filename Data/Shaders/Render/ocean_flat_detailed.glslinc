###VERTEX-120

#define in attribute
#define out varying

// Inputs
in vec2 inOceanDetailed1;	// Position (vec2)
in vec4 inOceanDetailed2;	// yTexture (float, UNUSED), yBack/yMid/yFront (float, world y)

// Parameters
uniform float paramEffectiveAmbientLightIntensity;
uniform float paramOceanTransparency;
uniform vec3 paramOceanFlatColor;
uniform mat4 paramOrthoMatrix;

// Outputs
out vec4 oceanColor;
out vec4 yVectorNdc;

void main()
{
    // Calculate color
    oceanColor = vec4(paramOceanFlatColor.xyz, 1.0 - paramOceanTransparency);

    // Apply ambient light
    oceanColor = oceanColor * paramEffectiveAmbientLightIntensity;

    // Calculate position
    gl_Position = paramOrthoMatrix * vec4(inOceanDetailed1, -1.0, 1.0);

    // Pass y values to fragment shader, converting to NDC
    yVectorNdc = vec4(
        gl_Position.y,
        inOceanDetailed2.yzw * paramOrthoMatrix[1].y + vec3(paramOrthoMatrix[3].y));
}


###FRAGMENT-120

#define in varying

#include "ocean_surface.glslinc"

// Inputs from previous shader
in vec4 oceanColor;
in vec4 yVectorNdc;

void main()
{
    gl_FragColor = CalculateOceanPlaneColor(oceanColor, yVectorNdc.x, yVectorNdc.y, yVectorNdc.z, yVectorNdc.w);
}
