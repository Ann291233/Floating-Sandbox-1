###VERTEX-120

#define in attribute
#define out varying

// Inputs
in vec2 inNpcAttributeGroup1; // Position
in vec4 inNpcAttributeGroup2; // PlaneId, OverlayColor
in vec4 inNpcAttributeGroup3; // Alpha, VertexSpacePosition, Light
#ifdef WITH_ROLES
in vec3 inNpcAttributeGroup4; // RoleColor
#endif

// Outputs
out float vertexWorldY;
out vec2 vertexSpacePosition;
out float vertexAlpha;
out float vertexLight;
out vec3 vertexOverlayColor;
#ifdef WITH_ROLES
out vec3 vertexRoleColor;
#endif

// Params
uniform mat4 paramOrthoMatrix;

void main()
{
    vertexWorldY = inNpcAttributeGroup1.y;
    vertexSpacePosition = inNpcAttributeGroup3.yz;
    vertexAlpha = inNpcAttributeGroup3.x;
    vertexLight = inNpcAttributeGroup3.w;
    vertexOverlayColor = inNpcAttributeGroup2.yzw;
#ifdef WITH_ROLES
    vertexRoleColor = inNpcAttributeGroup4;
#endif

    gl_Position = paramOrthoMatrix * vec4(inNpcAttributeGroup1, inNpcAttributeGroup2.x, 1.0);
}

###FRAGMENT-120

#define in varying

#include "common.glslinc"
#include "lamp_tool.glslinc"

// Inputs from previous shader
in float vertexWorldY;
in vec2 vertexSpacePosition; // [(-1.0, -1.0), (1.0, 1.0)]
in float vertexAlpha;
in float vertexLight;
in vec3 vertexOverlayColor;
#ifdef WITH_ROLES
in vec3 vertexRoleColor;
#endif

// Parameters
uniform float paramEffectiveAmbientLightIntensity;
uniform vec3 paramLampLightColor;
uniform float paramOceanDepthDarkeningRate;
uniform float paramShipDepthDarkeningSensitivity;
#ifndef WITH_ROLES
uniform vec3 paramNpcQuadFlatColor;
#endif

void main()
{
    vec2 uv = vec2(pow(vertexSpacePosition.x, 3.0), vertexSpacePosition.y);
    
    float d = distance(uv, vec2(.0, .0));    
    float alpha = 1.0 - smoothstep(0.87, 1.0, d);
    float borderAlpha = alpha - (1.0 - smoothstep(0.55, 0.8, d));

    // Darken when close to -1
    vec2 darkening = vec2(1.) - smoothstep(0.1, 1.0, -vertexSpacePosition);
    float darkeningScalar = min(darkening.x, darkening.y);
    
#ifdef WITH_ROLES
    vec3 cInner = vertexRoleColor;
#else
    vec3 cInner = paramNpcQuadFlatColor;
#endif
    vec3 cBorder = vec3(0.10, 0.10, 0.10);
    vec4 c = vec4(
        mix(cInner, cBorder, borderAlpha) * darkeningScalar,
        alpha);

    // Fragments with alpha lower than this are discarded, as we don't Z-sort NPCs
    #define MinAlpha 0.15
    if (c.a < MinAlpha)
        discard;

    // Apply highlight (overlay blending mode)
    //
    // (Target > 0.5) * (1 â€“ (1-2*(Target-0.5)) * (1-Blend)) +
    // (Target <= 0.5) * lerp(Target, Blend, alphaMagic)

    vec3 IsTargetLarge = step(vec3(0.5), c.rgb);
    vec3 TargetHigh = 1. - (1. - (c.rgb - .5) * 2.) * (1. - vertexOverlayColor.rgb);
    vec3 TargetLow = mix(c.rgb, vertexOverlayColor.rgb, 0.6);

    vec3 ovCol =
        IsTargetLarge * TargetHigh
        + (1. - IsTargetLarge) * TargetLow;

    c.rgb = mix(
        c.rgb,
        ovCol,
        step(0.0001, vertexOverlayColor.r + vertexOverlayColor.g + vertexOverlayColor.b) * c.a);

    // Complement missing ambient light with point's light
    float effectiveAmbientLightIntensity = mix(vertexLight, 1.0, paramEffectiveAmbientLightIntensity);

    // Calculate lamp tool intensity
    float lampToolIntensity = CalculateLampToolIntensity(gl_FragCoord.xy);

    if (paramShipDepthDarkeningSensitivity > 0.0) // Fine to branch - all pixels will follow the same branching
    {
        // Calculate depth darkening
        float darkeningFactor = CalculateOceanDepthDarkeningFactor(
            vertexWorldY,
            paramOceanDepthDarkeningRate);

        // Apply depth darkening
        c.rgb = mix(
            c.rgb,
            vec3(0.),
            darkeningFactor * (1.0 - lampToolIntensity) * (1.0 - vertexLight) * paramShipDepthDarkeningSensitivity);
    }

    // Apply ambient/lamp light
    // (complement missing ambient light with point's light_
    c.rgb *= max(
        mix(
            vertexLight, 
            1.0, 
            paramEffectiveAmbientLightIntensity), 
        lampToolIntensity);        

    // Apply electric lamp color
    c.rgb = mix(c.rgb, paramLampLightColor, vertexLight);

    c.a *= vertexAlpha;

    gl_FragColor = c;
} 
