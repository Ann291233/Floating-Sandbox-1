###VERTEX-120

#define in attribute
#define out varying

// Inputs
in vec3 inOceanDetailed1;	// Position (vec2), yTexture (float)
in vec4 inOceanDetailed2;	// yBack/yMid/yFront/dYFront2 (float, world y)

// Parameters
uniform mat4 paramOrthoMatrix;

// Outputs
out vec2 shadowPosNdc;
out vec3 worldCoordsAndYTexture;
out vec4 yVectorWorld; // yBackWorld/yMidWorld/yFrontWorld/dYFront2World

void main()
{
    // Calculate position
    vec4 posNdc = paramOrthoMatrix * vec4(inOceanDetailed1.xy, -1.0, 1.0);
    gl_Position = posNdc;

    // Pass values to fragment shader
    shadowPosNdc = vec2(posNdc.x, posNdc.y - paramOrthoMatrix[3][1]);
    worldCoordsAndYTexture = inOceanDetailed1;
    yVectorWorld = inOceanDetailed2;
}


###FRAGMENT-120

#define in varying

#include "common.glslinc"
#include "ocean.glslinc"

// Inputs from previous shader
in vec2 shadowPosNdc;
in vec3 worldCoordsAndYTexture;
in vec4 yVectorWorld; // yBackWorld/yMidWorld/yFrontWorld/dYFront2World

// Input textures
uniform sampler2D paramNoiseTexture;
uniform sampler2D paramOceanTexture;
uniform sampler1D paramSharedTexture;

// Parameters
uniform float paramEffectiveAmbientLightIntensity;
uniform vec3 paramEffectiveMoonlightColor;
uniform float paramOceanDarkeningRate;
uniform float paramOceanTransparency;
uniform float paramSunRaysInclination; // 0.0==vertical, 1.0/-1.0==45/-45 degrees
uniform vec2 paramTextureScaling;

void main()
{
    // Get texture color sample
    vec2 textureCoord2 = vec2(worldCoordsAndYTexture.x, worldCoordsAndYTexture.z);
    vec4 textureColor = texture2D(paramOceanTexture, textureCoord2 * paramTextureScaling);

    // Apply shadows
    float shadowFactor = GetShadowFactor(
        shadowPosNdc,
        worldCoordsAndYTexture.y,
        paramSunRaysInclination,
        paramSharedTexture,
        paramNoiseTexture);
    // Adjust shadow's dynamic range: [0.0, 1.0, 2.0] -> [0.0, 1.0, 1.4]
    shadowFactor = 1.3 * shadowFactor - 0.3 * shadowFactor * shadowFactor;
    textureColor = vec4(textureColor.xyz * shadowFactor, textureColor.w * (1.0 - paramOceanTransparency));

    // Apply ambient light
    textureColor.xyz = ApplyAmbientLight(
        textureColor.xyz, 
        paramEffectiveMoonlightColor, 
        paramEffectiveAmbientLightIntensity);

    // Apply depth darkening
    textureColor.xyz = ApplyDepthDarkening(
        textureColor.xyz,
        vec3(0.0),
        worldCoordsAndYTexture.y,
        paramOceanDarkeningRate);

    // Apply detail
    gl_FragColor = CalculateDetailedOceanPlaneColor(
        vec4(textureColor.xyz, textureColor.w * (1.0 - paramOceanTransparency)),
        worldCoordsAndYTexture.y,
        yVectorWorld, 
        paramEffectiveMoonlightColor, 
        paramEffectiveAmbientLightIntensity);
}
