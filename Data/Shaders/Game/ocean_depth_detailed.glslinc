###VERTEX-120

#define in attribute
#define out varying

// Inputs
in vec2 inOceanDetailed1;	// Position (vec2)
in vec4 inOceanDetailed2;	// yBack/yMid/yFront/dYFront2 (float, world y)

// Parameters
uniform mat4 paramOrthoMatrix;

// Outputs
out vec2 shadowPosNdc;
out float yWorld;
out vec4 yVectorWorld; // yBackWorld/yMidWorld/yFrontWorld/dYFront2World

void main()
{    
    // Calculate position
    vec4 posNdc = paramOrthoMatrix * vec4(inOceanDetailed1.xy, -1.0, 1.0);
    gl_Position = posNdc;

    // Pass values to fragment shader
    shadowPosNdc = vec2(posNdc.x, posNdc.y - paramOrthoMatrix[3][1]);
    yWorld = inOceanDetailed1.y;
    yVectorWorld = inOceanDetailed2;
}


###FRAGMENT-120

#define in varying

#include "common.glslinc"
#include "ocean.glslinc"

// Inputs from previous shader
in vec2 shadowPosNdc;
in float yWorld;
in vec4 yVectorWorld; // yBackWorld/yMidWorld/yFrontWorld/dYFront2World

// Input textures
uniform sampler2D paramNoiseTexture;
uniform sampler1D paramSharedTexture;

// Parameters
uniform float paramEffectiveAmbientLightIntensity;
uniform vec3 paramEffectiveMoonlightColor;
uniform float paramOceanTransparency;
uniform vec3 paramOceanDepthColorStart;
uniform vec3 paramOceanDepthColorEnd;
uniform float paramOceanDarkeningRate;
uniform float paramSunRaysInclination; // 0.0==vertical, 1.0/-1.0==45/-45 degrees

void main()
{
    // Do depth gradient
    vec3 depthColor = ApplyDepthDarkeningWithDither(
        paramOceanDepthColorStart,
        paramOceanDepthColorEnd,
        yWorld,
        paramOceanDarkeningRate,
        gl_FragCoord.xy,
        paramNoiseTexture);

    // Apply shadows
    float shadowFactor = GetShadowFactor(
        shadowPosNdc,
        yWorld,
        paramSunRaysInclination,
        paramSharedTexture,
        paramNoiseTexture);
    // Adjust shadow's dynamic range: [0.0, 1.0, 2.0] -> [0.0, 1.0, 1.4]
    shadowFactor = 1.3 * shadowFactor - 0.3 * shadowFactor * shadowFactor;
    depthColor = depthColor * shadowFactor;

    // Apply ambient light
    depthColor = ApplyAmbientLight(
        depthColor, 
        paramEffectiveMoonlightColor, 
        paramEffectiveAmbientLightIntensity);

    // Apply detail
    gl_FragColor = CalculateDetailedOceanPlaneColor(
        vec4(depthColor, (1.0 - paramOceanTransparency)),
        yWorld,
        yVectorWorld, 
        paramEffectiveMoonlightColor, 
        paramEffectiveAmbientLightIntensity);
}
